import "../Constants.bs"

sub init()
    m.top.functionName = "getData"
end sub

sub getData()
    city = m.top.city

    if city <> invalid and city <> ""
        url = WEATHER_BASE_URL+"/weather/video/" + city
        videoData = executeGetRequest(url)

        if videoData <> invalid then m.top.videoContent = setUpVideoContent(videoData)
    end if
end sub

function executeGetRequest(url as String) as Object
    port = CreateObject("roMessagePort")
    request = setUpHttpRequest(url)
    request.setMessagePort(port)
    request.AsyncGetToString()
    msg = wait(0, port)
    response = invalid
    
    if isSuccessfulHttpResponse(msg) then response = ParseJson(msg.GetString())

    return response
end function

function setUpHttpRequest(url as String) as Object
    request = CreateObject("roUrlTransfer")
    request.setMessagePort(CreateObject("roMessagePort"))
    request.SetUrl(url)
    request.SetCertificatesFile(CERTIFICATES_FILE)
    request.AddHeader(HEADER, "")
    request.InitClientCertificates()
    
    return request
end function

function isSuccessfulHttpResponse(msg as Object) as Boolean
    return type(msg) = "roUrlEvent" and msg.GetResponseCode() = 200
end function

function setUpVideoContent(videoData as Object) as Object
    itemContent = CreateObject("roSGNode","ContentNode")
    itemContent.url = videoData.video_url

    return itemContent
end function
