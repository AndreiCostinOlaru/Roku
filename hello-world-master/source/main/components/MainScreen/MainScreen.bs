import "../Services/AnimationService.bs"
import "../Services/NavigationService.bs"
import "../Constants.bs"

sub init()
  m.imageRowList = m.top.findNode("imageRowList")
  m.buttonVideo = m.top.findNode("buttonVideo")
  m.layoutGroup = m.top.findNode("layoutGroup")
  m.buttonMarkupGridScreen = m.top.findNode("buttonMarkupGridScreen")
  m.buttonKeyboardScreen = m.top.findNode("buttonKeyboardScreen")

  m.navigationService = new NavigationService(m.top)

  initialSetup()
end sub

sub initialSetup()
  initializeUI()
  initializeTasks()
  initializeAnimations()
  initializeTimer()
  setUpObservedFields()
  runTasks()
end sub

sub initializeUI()
  m.imageRowList.setFocus(true)
  m.imageRowList.content = populateImageRowList()
end sub

sub initializeTasks()
  m.getPokemonDataTask = CreateObject("roSGNode", "GetPokemonDataTask")
  m.getVideoData = CreateObject("roSGNode", "GetVideoDataTask")
end sub

sub initializeAnimations()
  origin = ScreenPoint(1000, 1000)
  destination = ScreenPoint(1000, 0)
  m.animationService = new AnimationService(origin, destination)
  m.translationAnimation = m.animationService.createButtonsRowListAnimation(m.layoutGroup)
end sub

sub initializeTimer()
  m.rowListFocusTimer = createTimer(5, true)
  startTimer(m.rowListFocusTimer)
end sub

sub setUpObservedFields()
  m.buttonVideo.ObserveField("buttonSelected", "onButtonVideoSelected")
  m.imageRowList.ObserveField("rowItemSelected", "onRowItemSelected")
  m.getPokemonDataTask.ObserveField("itemContent", "onFetchPokemonData")
  m.getVideoData.ObserveField("itemContent", "onFetchVideoData")
  m.buttonMarkupGridScreen.observeField("buttonSelected", "onButtonMarkupGridScreenSelected")
  m.rowListFocusTimer.ObserveField("fire", "onRowListTimer")
  m.imageRowList.ObserveField("rowItemFocused", "onRowListItemFocused")
  m.buttonKeyboardScreen.ObserveField("buttonSelected", "onButtonKeyboardScreenSelected")
end sub

sub runTasks()
  m.getPokemonDataTask.control = "RUN"
  m.getVideoData.control = "RUN"
end sub

sub onFetchPokemonData(event as Object)
  rowContent = event.getData()
  rowListRootContent = CreateObject("roSGNode","ContentNode")
  rowListRootContent.appendChild(rowContent)
  m.imageRowList.content = rowListRootContent
  m.markupGridData = rowContent
  m.top.signalBeacon("AppLaunchComplete")
end sub

sub onFetchVideoData(event as Object)
  m.videoData = event.getData()
end sub

sub moveFocusToButtons()
  m.animationService.reverseStartAnimation(m.translationAnimation)
  m.buttonVideo.setFocus(true)
  stopTimer(m.rowListFocusTimer)
end sub

sub moveFocusToRowList()
  m.animationService.reverseStartAnimation(m.translationAnimation)
  m.imageRowList.setFocus(true)
  startTimer(m.rowListFocusTimer)
end sub

sub onButtonVideoSelected()
  m.navigationService.navigateTo("VideoScreen", m.videoData, m.buttonVideo)
end sub

sub onButtonMarkupGridScreenSelected()
  m.navigationService.navigateTo("MarkupGridScreen", m.markupGridData, m.buttonMarkupGridScreen)
end sub

sub onButtonKeyboardScreenSelected()
  m.navigationService.navigateTo("KeyboardScreen", m.markupGridData, m.buttonKeyboardScreen)
end sub

sub onRowItemSelected(event as Object)
  data = event.getData()
  screenContent = m.imageRowList.content.getChild(data[0]).getChild(data[1])
  stopTimer(m.rowListFocusTimer) 
  m.navigationService.navigateTo("DescriptionScreen", screenContent, m.imageRowList)
end sub

sub onMainSceneSuspend(args as dynamic)
  if m.newVideoScreen <> invalid
    player = m.newVideoScreen.findNode("video")
    player.control = "stop"
    m.top.removeChild(m.newVideoScreen)
    m.buttonVideo.setFocus(true)
    m.newVideoScreen = invalid
  end if
end sub

sub onMainSceneResume(args as dynamic)
  m.top.signalBeacon("AppResumeComplete")
end sub

sub onRowListTimer(event as Object)
  indexFocusedItem = m.imageRowList.rowItemFocused[1]
  newIndexFocusedItem = (indexFocusedItem + 1) mod 7
  m.imageRowList.jumpToRowItem = [0, newIndexFocusedItem]
end sub

sub onRowListItemFocused(event as Object)
  resetTimerDuration(m.rowListFocusTimer, 5)
end sub

sub startTimer(timer as Object)
  timer.control = "start"
end sub

sub stopTimer(timer as Object)
  timer.control = "stop"
end sub

sub resetTimerDuration(timer as Object, duration as Integer)
  stopTimer(timer)
  timer.duration = duration
  startTimer(timer)
end sub

function onKeyEvent(key as String, press as Boolean) as Boolean
  handled = false

  if press
    if key = "down" and m.imageRowList.hasFocus()
      handled = true
      moveFocusToButtons()
    else if key = "up" and isAnyButtonFocused()
      handled = true
      moveFocusToRowList()
    else if key = "right" and m.buttonVideo.hasFocus()
      handled = true
      m.buttonMarkupGridScreen.setFocus(true)
    else if key ="right" and m.buttonMarkupGridScreen.hasFocus()
      handled = true
      m.buttonKeyboardScreen.setFocus(true)
    else if key = "left" and m.buttonMarkupGridScreen.hasFocus()
      handled = true
      m.buttonVideo.setFocus(true)
    else if key = "left" and m.buttonKeyboardScreen.hasFocus()
      handled = true
      m.buttonMarkupGridScreen.setFocus(true)
    end if
  end if

  return handled
end function

function populateImageRowList() as Object
  rowListRootContent = CreateObject("roSGNode","ContentNode")
  rowContent = CreateObject("roSGNode","ContentNode")
  rowContent.TITLE = "Pokemons"

  for x = 1 To 5
    rowChild = createPosterPlaceholder()
    rowContent.appendChild(rowChild)
  end for

  rowListRootContent.appendChild(rowContent)

  return rowListRootContent
end function

function createPosterPlaceholder() as Object
  placeholder = CreateObject("roSGNode","ContentNode")
  placeholder.url = PLACEHOLDER_URL  

  return placeholder
end function

function isAnyButtonFocused() as Boolean
  return m.buttonVideo.hasFocus() or m.buttonMarkupGridScreen.hasFocus() or m.buttonKeyboardScreen.hasFocus()
end function

function createTimer(duration as integer, repeat as boolean)
  timer = createObject("roSGNode", "Timer")
  timer.duration = duration
  timer.repeat = repeat
  
  return timer
end function
