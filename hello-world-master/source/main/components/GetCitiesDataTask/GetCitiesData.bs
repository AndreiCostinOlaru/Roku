import "../Constants.bs"

sub init()
    m.top.functionName = "getCitiesData"
end sub

sub getCitiesData()
    citiesJson = executeGetRequest(WEATHER_BASE_URL + "/weather/cities")
    citiesContent = populateCitiesItems(citiesJson)
    m.top.citiesContent = citiesContent
end sub

function executeGetRequest(url as String) as Object
    port = CreateObject("roMessagePort")
    request = setUpHttpRequest(url)
    request.setMessagePort(port)
    request.AsyncGetToString()
    msg = wait(0, port)
    response = invalid
    
    if isSuccessfulHttpResponse(msg)
        response = ParseJson(msg.GetString())
    end if
    
    return response
end function

function setUpHttpRequest(url as String) as Object
    request = CreateObject("roUrlTransfer")
    request.setMessagePort(CreateObject("roMessagePort"))
    request.SetUrl(url)
    request.SetCertificatesFile(CERTIFICATES_FILE)
    request.AddHeader(HEADER, "")
    request.InitClientCertificates()

    return request
end function

function isSuccessfulHttpResponse(msg as Object) as Boolean
    return type(msg) = "roUrlEvent" and msg.GetResponseCode() = 200
end function

function populateCitiesItems(citiesJson as Object) as Object
    citiesContent = CreateObject("roSGNode","ContentNode")
    citiesContent.TITLE = "Cities"

    if citiesJson <> invalid and type(citiesJson) = "roArray"
        for each city in citiesJson
            cityChild = createCityItem(city)
            citiesContent.appendChild(cityChild)
        end for
    end if

    return citiesContent
end function

function createCityItem(cityName as String) as Object
    city = CreateObject("roSGNode","itemContentNode")
    city.title = cityName
    city.description = "Weather forecast for " + cityName
    city.FHDPOSTERURL = PLACEHOLDER_URL
    
    return city
end function
